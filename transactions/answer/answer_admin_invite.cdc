// answer_admin_invite.cdc
// Answer the invitation to be an Admin.

import DAAM from 0x7db4d10c78bad30a

transaction(submit: Bool) {
    let signer: AuthAccount
    let submit: Bool

    prepare(signer: AuthAccount) {
        self.signer = signer
        self.submit = submit     
    }

    execute {
        let admin <- DAAM.answerAdminInvite(newAdmin: self.signer, submit: self.submit)
        if admin != nil {
            let old_admin <- self.signer.load<@AnyResource>(from: DAAM.adminStoragePath)
            self.signer.save<@DAAM.Admin>(<- admin!, to: DAAM.adminStoragePath)
            let adminRef = self.signer.borrow<&DAAM.Admin>(from: DAAM.adminStoragePath)!
            destroy old_admin

            let old_request <- self.signer.load<@AnyResource>(from: DAAM.requestStoragePath)
            let requestGen <-! adminRef.newRequestGenerator()
            self.signer.save<@DAAM.RequestGenerator>(<- requestGen, to: DAAM.requestStoragePath)
            destroy old_request
            
            log("You are now a DAAM.Admin: ".concat(self.signer.address.toString()) )
        } else {
            destroy admin
            log("Thank You for your consoderation.")
        }
    }
}
